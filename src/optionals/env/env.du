import Env;

def readFile(path='.env') {
    const SPLIT_DELIMITER = '=';
    const COMMENT = '#';

    with(path, 'r') {
        var line;
        var lineCount = 0;
        // When you reach the end of the file, nil is returned
        while((line = file.readLine()) != nil) {
            lineCount = lineCount + 1;
            if (not line or line.startsWith(COMMENT))
                continue;

            if (not line.contains('='))
                return Error('Malformed entry on line {}'.format(lineCount));

            const [variable, rawValue] = line.split(SPLIT_DELIMITER, 1);
            // Strip out any in-line comments
            const value = rawValue.split(COMMENT, 1);
            const result = Env.set(variable.strip(), value[0].strip());

            if (not result.success()) {
                return result;
            }
        }
    }

    return Success(nil);
}