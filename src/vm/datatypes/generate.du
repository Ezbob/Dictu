import Path;

/**
 * This file is used to generate the source of any datatype
 * methods that are actually written in Dictu ready to be loaded
 * by the VM on init.
 */

def generate(filename) {
    var output;
    with("{}.du".format(filename), "r") {
        const basename = Path.basename(filename);
        const name = "{}{}".format(basename[0].upper(), basename[1:]);
        var line = file.readLine();
        output = 'char* dictu{}Source = "{}\\n"\n'.format(name, line);
        // When you reach the end of the file, nil is returned
        while ((line = file.readLine()) != nil) {
            output += '"{}\\n"\n'.format(line);
        }

        output += ";";
    }

    with("{}-source.c".format(filename), "w") {
        file.write(output);
    }
}

var files = [
    'lists/list'
];

for (var i = 0; i < files.len(); i += 1) {
    generate(files[i]);
}