#!/usr/local/bin/dictu

import Env;
import Process;
import System;


const PKG_FORMATS = ["deb", "rpm", "apk"],
      PKG_ARCHS = ["amd64"];

{ // main
    print("Setting up environment...");

    var semver = Env.get("SEMVER");
    if (not semver) {
        const GIT_REV = Process.run(["git", "rev-parse", "--short", "HEAD"], true);
        if (not GIT_REV.success()) {
            print(GIT_REV.unwrapError());
            System.exit(1);
        }
        semver = GIT_REV.unwrap();
        semver = semver.strip();
        Env.set("SEMVER", semver);
    }

    print("Building packages for version: {}".format(semver));

    PKG_FORMATS.forEach(def(pkgFmt) => {
        print("Creating {}...".format(pkgFmt));

        Process.run(["nfpm", "pkg", "-f", "ops/nfpm_amd64.yaml", "--packager", pkgFmt, "--target", "build"], true).match(
            def(result) => {},
            def(error) => {
                print(error);
                System.exit(1);  
            }
        );
    });

    print("Complete!");
    print("Packages can be found in ./build");

    System.exit(0);
}
